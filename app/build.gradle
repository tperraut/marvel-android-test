apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

apply from: '../config/quality.gradle'

// Load keystore
def keystorePropertiesFile = rootProject.file("app/keystore/keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "rocks.flawless.marveltestapp"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        release {
            storeFile file(keystoreProperties['releaseStoreFile'])
            storePassword keystoreProperties['releaseStorePassword']
            keyAlias keystoreProperties['releaseKeyAlias']
            keyPassword keystoreProperties['releaseKeyPassword']
        }
        debug {
            storeFile file(keystoreProperties['debugStoreFile'])
            storePassword keystoreProperties['debugStorePassword']
            keyAlias keystoreProperties['debugKeyAlias']
            keyPassword keystoreProperties['debugKeyPassword']
        }
    }
    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            debuggable true
            versionNameSuffix "-debug"
            applicationIdSuffix ".debug"
            signingConfig signingConfigs.debug
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard-rules.pro'
            debuggable false
            signingConfig signingConfigs.release
        }

    }

    flavorDimensions "api"

    productFlavors {
        production {
            dimension "api"
            buildConfigField "String", "API_URL", keystoreProperties['apiUrl']
            buildConfigField "String", "API_PUBLIC_KEY", keystoreProperties['apiPublicKey']
            buildConfigField "String", "API_PRIVATE_KEY", keystoreProperties['apiPrivateKey']
        }

        dev {
            dimension "api"
            buildConfigField "String", "API_URL", keystoreProperties['apiUrl']
            buildConfigField "String", "API_PUBLIC_KEY", keystoreProperties['apiPublicKey']
            buildConfigField "String", "API_PRIVATE_KEY", keystoreProperties['apiPrivateKey']
        }
    }

    splits {
        abi {
            enable true
            universalApk true
        }
        density {
            enable true
        }
    }

    androidExtensions {
        experimental = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0-alpha04'
    // /!\ activity-ktx alpha07 not compatible with appcompat alpha04 /!\
    implementation 'androidx.activity:activity-ktx:1.0.0-alpha06'
    implementation 'androidx.core:core-ktx:1.1.0-alpha05'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.recyclerview:recyclerview:1.1.0-alpha04'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.coordinatorlayout:coordinatorlayout:1.1.0-alpha01'
    implementation 'com.google.android.material:material:1.1.0-alpha05'
    /* Lifecycle */
    def lifecycle_version = "2.1.0-alpha04"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
    /* Testing */
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
    /* CoffeeScene */
    implementation 'com.geronimostudios.coffeescene:coffeescene:1.0.2'
    /* Fresco */
    implementation 'com.facebook.fresco:fresco:1.13.0'
    /* Coroutines */
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.1.1'
    /* Gson */
    implementation 'com.google.code.gson:gson:2.8.5'
    /* CoffeeScene */
    implementation 'com.geronimostudios.coffeescene:coffeescene:1.0.2'
    /* OkHttp */
    implementation "com.squareup.okhttp3:okhttp:3.14.1"
    implementation 'com.squareup.okhttp3:logging-interceptor:3.14.1'
    /* Retrofit */
    implementation 'com.squareup.retrofit2:retrofit:2.5.1-SNAPSHOT'
    implementation 'com.squareup.retrofit2:converter-gson:2.5.1-SNAPSHOT'
}
